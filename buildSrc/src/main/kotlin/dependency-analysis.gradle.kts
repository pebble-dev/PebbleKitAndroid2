import com.autonomousapps.DependencyAnalysisSubExtension

plugins {
    id("com.autonomousapps.dependency-analysis")
}

configure<DependencyAnalysisSubExtension> {
    issues {
        onAny {
            // Rebugger and WhatTheStack are always included as a convenience,
            // so it can immediately be used. It's debug only anyway
            exclude("io.github.theapache64:rebugger")
            exclude("com.github.fvito:WhatTheStack")

            // Included by kotlin automatically, we can't affect it
            exclude("org.jetbrains.kotlin:kotlin-stdlib")

            // False positives, maybe generated by the MoshiX?
            exclude("com.squareup.moshi:moshi")

            // Fail build if there are any violations
            severity("fail")
        }

        onUnusedDependencies {
            // Standard test dependencies, included in all modules by default for huge convenience boost
            exclude("org.junit.jupiter:junit-jupiter-api")
            exclude("io.kotest:kotest-assertions-core")
            exclude("org.jetbrains.kotlinx:kotlinx-coroutines-test")
            exclude("app.cash.turbine:turbine")
            exclude("si.inova.kotlinova:navigation-test")

            // Standard sqldelight dependencies, always included for convenience
            exclude("app.cash.sqldelight:async-extensions")

            // Standard compose dependencies, always included for convenience
            exclude("androidx.compose.ui:ui-graphics")
            exclude("androidx.compose.ui:ui-test-junit4")
            exclude("androidx.compose.material3:material3")
            exclude("androidx.compose.material:material")
            exclude("androidx.compose.ui:ui-tooling-preview")
            exclude("androidx.lifecycle:lifecycle-runtime-compose")
            exclude("si.inova.kotlinova:compose")

            // This is fine, included with anvil
            exclude("software.amazon.lastmile.kotlin.inject.anvil:runtime-optional")
        }

        onIncorrectConfiguration {
            // Showkase is only used by the generated code, by app module that also needs to explicitly include showkase
            exclude("com.airbnb.android:showkase")

            // Navigation should be included as-needed to reduce already huge amount of modules depending on it
            exclude(":common-navigation")
        }

        onUsedTransitiveDependencies {
            // This is fine, included with sqldelight
            exclude("androidx.sqlite:sqlite")

            // This is fine, included with paparazzi
            exclude("com.android.tools.layoutlib:layoutlib-api")
            exclude("com.android.tools.layoutlib:layoutlib")
            exclude("app.cash.paparazzi:layoutlib-native-jdk11")

            // This is fine, included with parcelize plugin
            exclude("org.jetbrains.kotlin:kotlin-parcelize-runtime")

            // This is fine, included with detekt API
            exclude("org.jetbrains.kotlin:kotlin-compiler-embeddable")

            // This is fine, included with kotlinova navigation
            exclude("com.github.Zhuinden:simple-stack")

            // AndroidX Annotations are auto-included with many other AndroidX libraries.
            // It's fine to not explicitly depend on them
            exclude("androidx.annotation:annotation")
        }
    }
}
